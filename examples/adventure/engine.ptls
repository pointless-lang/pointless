fn addPath(game, source, dest, label)
  game.paths |= push({ source, dest, label })
  game
end

fn update(game)
  clear()
  print(game.rooms[game.current])
  print("\nYour options are:\n")

  paths = game.paths ? arg.source == game.current

  if isEmpty(paths) then
    paths |= push({ dest: "start", label: "Restart" })
  end

  paths |= push({ dest: none, label: "Exit" })

  index = 1
  for path in paths do
    print("($index) $path.label")
    index += 1
  end

  choice = prompt("\nEnter a number: ")
  game.current = paths[math.toNum(choice) - 1].dest
  game
end

fn play(game)
  game.current = "start"

  while game.current != none do
    game |= update
  end
end

newGame = { rooms: {}, paths: [], current: none }

{ newGame, addPath, play }
