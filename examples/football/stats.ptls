games = import "csv:games.csv"

fn splitGame(game)
  #{
    team          , scored         , allowed
    game.homeTeam , game.homeScore , game.awayScore
    game.awayTeam , game.awayScore , game.homeScore
  }
end

fn getStats(group)
  numGames = len(group)

  wins = group
    ? arg.scored > arg.allowed
    | len

  losses = group
    ? arg.scored < arg.allowed
    | len

  ties = group
    ? arg.scored == arg.allowed
    | len

  winPct = wins / numGames * 100
  scored = sum(group.scored)
  allowed = sum(group.allowed)
  scoredPG = scored / numGames
  allowedPG = allowed / numGames
  diff = scored - allowed

  { wins, losses, ties, winPct, scoredPG, allowedPG, diff }
end

games
  $ splitGame
  | Table.merge
  | Table.summarize("team", getStats)
  | sortDescBy("winPct")
  | Table.roundTo(1)
  | print
