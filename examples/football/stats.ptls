games = import "csv:games.csv"

fn splitGame(game)
  #{
    team          , scored         , allowed
    game.homeTeam , game.homeScore , game.awayScore
    game.awayTeam , game.awayScore , game.homeScore
  }
end

fn getStats(group)
  wins = len(group ? arg.scored > arg.allowed)
  losses = len(group ? arg.scored < arg.allowed)
  ties = len(group ? arg.scored == arg.allowed)
  
  denom = Math.max(1, len(group))
  winPct = wins / denom
  scored = sum(group.scored)
  allowed = sum(group.allowed)
  scoredPG = scored / denom
  allowedPG = allowed / denom
  diff = scored - allowed

  { wins, losses, ties, winPct, scoredPG, allowedPG, diff }
end

games
  $ splitGame
  | Table.merge
  | Table.summarize("team", getStats)
  | sortDescBy("winPct")
  | Table.sigFigs(2)
  | print
