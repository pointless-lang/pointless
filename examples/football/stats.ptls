games = import "csv:games.csv"

fn splitGame(game)
  #{
    team          , scored         , allowed
    game.homeTeam , game.homeScore , game.awayScore
    game.awayTeam , game.awayScore , game.homeScore
  }
end

fn getStats(group)
  denom = Math.max(1, len(group))

  wins = len(group ? arg.scored > arg.allowed)
  losses = len(group ? arg.scored < arg.allowed)
  ties = len(group ? arg.scored == arg.allowed)
  scored = sum(group.scored)
  allowed = sum(group.allowed)
  
  winPct = wins / denom
  scoredPG = scored / denom
  allowedPG = allowed / denom
  diff = scored - allowed

  { wins, losses, ties, winPct, scoredPG, allowedPG, diff }
end

games
  $ splitGame
  | Table.merge
  | Table.summarize("team", getStats)
  | sortDescBy("winPct")
  | roundTo({ winPct: 3, scoredPG: 1, allowedPG: 1 })
  | print
